! ploDataElem is a post-processing software for T+H (TOUGH HYDRATE) program.
! It reads plot_data_elem file generated by T+H simulations and converts them
! vtu format. Which can then be visualized by usign PARAVIEW and other
! softwares.
! Copyright (C) 2020-2021  Vikas Sharma, Ph.D
!
! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <https: //www.gnu.org/licenses/>
!

SUBMODULE( PlotDataElem_Class ) Methods
IMPLICIT NONE
INTEGER( I4B ) :: msg_no = 0

INTERFACE Display
  MODULE PROCEDURE Display_str
END INTERFACE

CONTAINS

SUBROUTINE Display_str(msg)
  CHARACTER( LEN = * ), INTENT( IN ) :: msg
  msg_no = msg_no + 1
  WRITE( *, "(A, I3, A, A)") "[", msg_no, "]", TRIM(MSG)
END SUBROUTINE Display_str

!----------------------------------------------------------------------------
!
!----------------------------------------------------------------------------

SUBROUTINE Error_MSG_1
  WRITE( *, "(A)" ) "FATAL ERROR :: In File PlotDataElem_Class@Methods.f90"
  WRITE( *, "(A)" ) "In Subroutine init_plotDataElem(Obj, FileName, &
    & varName, dim, isAxiSym, tElements, ierr)"
  WRITE( *, "(A)" ) "Following error message received :: "
  WRITE( *, "(A)" ) TRIM(ERRMSG_1)
  WRITE( *, "(A)" ) "PROGRAM STOPPED !!!"
END SUBROUTINE

!----------------------------------------------------------------------------
!
!----------------------------------------------------------------------------

SUBROUTINE Error_MSG_2
  WRITE( *, "(A)" ) "FATAL ERROR :: In File PlotDataElem_Class@Methods.f90"
  WRITE( *, "(A)" ) "In Subroutine init_plotDataElem(Obj, FileName, &
    & varName, dim, isAxiSym, tElements, ierr)"
  WRITE( *, "(A)" ) "Following error message received :: "
  WRITE( *, "(A)" ) TRIM(ERRMSG_2)
  WRITE( *, "(A)" ) "PROGRAM STOPPED !!!"
END SUBROUTINE

!----------------------------------------------------------------------------
!
!----------------------------------------------------------------------------

MODULE PROCEDURE init_plotDataElem
  CHARACTER( LEN = MAX_LEN_BUFFER ) :: buffer
  INTEGER( I4B ) :: IOSTAT
  INTEGER( I4B ) :: tZone
  INTEGER( I4B ) :: i
  INTEGER( I4B ) :: j
  INTEGER( I4B ) :: n
  INTEGER( I4B ) :: ZoneId
  INTEGER( I4B ) :: tPoints
  REAL( DFP ) :: t
  REAL( DFP ) :: xyz( 3 )
  REAL( DFP ), ALLOCATABLE :: varValue( : )
  REAL( DFP ), ALLOCATABLE :: tempMat( :, : )
  CHARACTER( LEN = 15 ) :: myStr


  ! varName
  n = SIZE( varName )
  Obj % tVar = n

  CALL Display("Checking variable names")
  IF( n .GT. MAX_VAR_NAME_ALLOWED ) THEN
    CALL Error_MSG_1; STOP
  END IF

  CALL Display("Setting variable names")
  DO i = 1, n
    Obj % varName( i ) = varName( i )
  END DO

  ALLOCATE( varValue( n ) )
  varValue = 0.0_DFP

  CALL Display("Setting dimension of grid")
  Obj % dim = dim

  CALL Display("Checking if AxisSymmetric")
  Obj % isAxiSym = isAxiSym
  IF( Obj % isAxiSym ) THEN
    WRITE( *, "(A)") "AxiSymmetric FOUND"
  ELSE
    WRITE( *, "(A)") "AxiSymmetric NOT FOUND"
  END IF

  CALL Display("Opening Plot_Data_Elem file from")
  WRITE( *, "(A)" ) "Path of the file :: "//TRIM(FileName(1)%chars())
  WRITE( *, "(A)" ) "Name of the file :: "//TRIM(FileName(2)%chars())
  WRITE( *, "(A)" ) "Extension of the file :: "//TRIM(FileName(3)%chars())
  CALL OpenFileToRead( Obj = Obj % aFile,  PFE = FileName )

  CALL Display( "Finding total number of zone in PlotDataElem")
  tZone = 0
  DO
    READ( Obj % aFile % UnitNo, "(A)", IOSTAT = IOSTAT ) buffer
    ! check end of file
    IF( IOSTAT .LT. 0 ) THEN
      EXIT
    ELSE IF( buffer( 1:4 ) .EQ. 'ZONE' ) THEN
      tZone = tZone + 1
    END IF
  END DO
  WRITE( *, "(A, I6)" ) "Total number of zones are :: ", tZone

  CALL Display( "Allocating Obj%Zone" )
  IF( ALLOCATED( Obj % zone ) ) DEALLOCATE( Obj % zone )
  IF( tZone .NE. 0 ) THEN
    ALLOCATE( Obj % zone( tZone ) )
  ELSE
    CALL Error_MSG_2
    STOP
  END IF

  CALL Display("Reopening Plot_Data_Elem file")
  CALL ReOpenFile( Obj % aFile )
  ZoneId = 0

  DO

    READ( Obj % aFile % UnitNo, "(A)", IOSTAT = IOSTAT  ) buffer
    IF( IOSTAT .LT. 0 ) EXIT

    IF( buffer( 1:4 ) .EQ. "ZONE" ) THEN

      ZoneId = ZoneId + 1

      WRITE( *, "(A, I4)") "Inside Zone :: ", ZoneId

      myStr = buffer( 10 : 24 )
      Obj % zone( ZoneId ) % time =  cton( myStr, 1.0 )
      Obj % zone( ZoneId ) % Format( 1 : 5 ) = buffer( 30 : 34 )

      IF( PRESENT( tElements ) ) THEN
        tPoints = tElements
      ELSE
        myStr = buffer( (LEN_TRIM( buffer ) - 5) : LEN_TRIM( buffer ) )
        tPoints = cton( myStr, 1_I4B )
      END IF

      WRITE( *, "(A)") "Setting total number of points"
      Obj % zone( ZoneId ) % tPoints = tPoints

      WRITE( *, "(A)") "Allocating memory for nodal coords"
      ALLOCATE( Obj % zone( ZoneId ) % xyz( 1:3, 1:tPoints ) )
      Obj % zone( ZoneId ) % xyz = 0.0_DFP

      WRITE( *, "(A)") "Allocating memory for variables"
      ALLOCATE( Obj % zone( ZoneId ) % varValue( n, tPoints ) )
      Obj % zone( ZoneId ) % varValue = 0.0_DFP

      WRITE( *, "(A)") "Reading nodes and variable information from file"
      IF( isAxiSym ) THEN
        DO i = 1, tPoints
          ! read data r and z
          READ( Obj % aFile % UnitNo, * ) xyz( 1 ), xyz( 2 ), &
            & (varValue( j ), j = 1, n)
          Obj % zone( ZoneId ) % xyz( 1:2, i ) = xyz( 1:2 )
          Obj % zone( ZoneId ) % varValue( :, i ) = varValue
        END DO
      ELSE
        DO i = 1, tPoints
          ! read data x, y, z
          READ( Obj % aFile % UnitNo, * ) xyz( 1 ), xyz( 2 ), xyz( 3 ), &
            & (varValue( j ), j = 1, n)
          Obj % zone( ZoneId ) % xyz( :, i ) = xyz
          Obj % zone( ZoneId ) % varValue( :, i ) = varValue
        END DO
      END IF
    END IF
  END DO

  ! IF( dim .EQ. 1 ) THEN
  !   DO i = 1, tZone
  !     Obj % zone( i ) % tPoints = Obj % zone( i ) % tPoints * 2
  !     tempMat = Obj % zone( i ) % varValue
  !     DEALLOCATE( Obj % zone( i ) % varValue )
  !     ALLOCATE( Obj % zone( i ) % varValue( n, 2 * tPoints ) )
  !     Obj % zone( i ) % varValue( 1 : n, 1 : tPoints ) = tempMat
  !     Obj % zone( i ) % varValue( 1 : n, tPoints + 1 : 2 * tPoints ) = tempMat
  !     tempMat = Obj % zone( i ) % xyz
  !     DEALLOCATE( Obj % zone( i ) % xyz )
  !     ALLOCATE( Obj % zone( i ) % xyz( 3, 2 * tPoints ) )
  !     Obj % zone( i ) % xyz( 1 : 3, 1 : tPoints ) = tempMat
  !     Obj % zone( i ) % xyz( 1 : 3, tPoints + 1 : 2 * tPoints ) = tempMat
  !     Obj % zone( i ) % xyz( 2, tPoints + 1 : 2 * tPoints ) = &
  !       & Obj % zone( i ) % xyz( 2, tPoints + 1 : 2 * tPoints ) + 1.0
  !   END DO
  ! END IF
END PROCEDURE init_plotDataElem

!----------------------------------------------------------------------------
!                                                                    Display
!----------------------------------------------------------------------------

MODULE PROCEDURE disp_plotDataElem
  INTEGER( I4B ) :: I, j, k, n
  !
  IF( PRESENT( UnitNo ) ) THEN
    I = UnitNo
  ELSE
    I = 6
  END IF
  !
  IF( LEN_TRIM( Msg ) .NE. 0 ) THEN
    WRITE( I, "(A)" ) TRIM( Msg )
  END IF
  !
  n = Obj%tVar
  DO j = 1, n
    WRITE( I, "(2A)", ADVANCE="NO" ) TRIM( Obj % varName( j ) % Raw ), "  "
  END DO
  WRITE( I, "(A)" ) ""
  !
  DO j = 1, SIZE( Obj % zone )
    WRITE( I, "(A, E14.4, A, A, A, I6 )") "ZONE T = ", Obj % zone( j ) % time, &
      & "    Format = ", Obj % zone( j ) % Format, &
      & "    Total Points = ", Obj % zone( j ) % tPoints
    DO k = 1, Obj % zone( j ) % tPoints
      WRITE( I,  *  ) Obj % zone( j ) % xyz( 1:3, k ),  &
        & Obj % zone( j ) % varValue( 1 : n, k )
    END DO
    WRITE( I, * ) ""
    WRITE( I, * ) ""
    WRITE( I, * ) ""
  END DO
END PROCEDURE disp_plotDataElem

!----------------------------------------------------------------------------
!
!----------------------------------------------------------------------------

MODULE PROCEDURE write_nodes
  TYPE( String ) :: fileName( 3 )
  TYPE( File_ ) :: aFile
  INTEGER( I4B ) :: i, dim
  !
  IF( PRESENT( path ) ) THEN
    fileName( 1 ) = String( path )
  ELSE
    fileName( 1 ) = String( "./" )
  END IF
  !
  IF( PRESENT( prefix ) ) THEN
    fileName( 2 ) = String( &
      & TRIM( prefix ) // "_" // TRIM( Str( n = ZoneID, no_sign = .true. ) ) // "_nodes" )
  ELSE
    fileName( 2 ) = String( &
      & "Zone_" // TRIM( Str( n = ZoneID, no_sign = .true. ) ) // "_nodes" )
  END IF
  !
  fileName( 3 ) = String( ".txt" )
  CALL OpenFileToWrite( aFile, fileName )

  dim = MAX( Obj % dim, 2 )
  DO i = 1, Obj % zone( ZoneID ) % tPoints
    WRITE( aFile % UnitNo, "(3E14.4)" ) Obj % zone( ZoneID ) % xyz( 1:dim, i )
  END DO
  !
  CALL CloseFile( aFile )
  !
END PROCEDURE write_nodes

END SUBMODULE Methods